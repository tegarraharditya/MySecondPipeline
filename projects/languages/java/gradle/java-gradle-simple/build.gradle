// Uses DSL plugins resolution introduced in Gradle 2.1
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
        classpath 'ru.d10xa:gradle-allure-plugin:0.5.2'
    }
}

plugins {
  id "java"
  id "jacoco"
  id "org.sonarqube" version "1.2"
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'ru.d10xa.allure'
}

configurations {
    agent
}

allure {
    aspectjweaver = true
    testNG = true
    allureResultsDir = "${projectDir}/target/allure-results"
    allureReportDir = "${projectDir}/target/allure-report"
    allureVersion = "${allureVersion}"
    aspectjVersion = "${aspectjVersion}"
    clean = true
}

dependencies {
    agent "org.aspectj:aspectjweaver:${aspectjVersion}"
    testCompile "ru.yandex.qatools.allure:allure-testng-adaptor:${allureVersion}"
    testCompile 'org.testng:testng:6.9.10'
}


task customZip(type: Zip) {
    from 'src/'
    archiveName 'mysonar-example.zip'
    baseName 'mysonar-example'
}

artifacts {
    archives customZip
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'gradle-dev-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
            
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-dev'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
            
        }
    }
}


sonarqube {
    properties {
        property "sonar.projectName", "Java :: Simple Project :: SonarQube Scanner for Gradle"
        property "sonar.projectKey", "org.sonarqube:java-gradle-simple"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}

allprojects {
  ext.baseVersion = "0.1"
  ext.snapshotVersion = true

  group = "org.sonarqube"
  version = "$baseVersion" + (snapshotVersion ? "-SNAPSHOT" : "")
}

test.doFirst {
    jvmArgs "-javaagent:${configurations.agent.singleFile}"
}

test {
  ignoreFailures = false
  useTestNG() {
//        suiteXmlFiles << new File(rootDir, "testng.xml") //This is how to add custom testng.xml
  }
}

repositories {
  repositories {
    maven {
      url "http://repo1.maven.org/maven2/"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
}

tasks.withType(Test)*.finalizedBy allureReport
